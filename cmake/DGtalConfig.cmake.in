# - Config file for the DGtal package
# It defines the following variables
#  DGTAL_INCLUDE_DIRS - include directories for DGtal
#  DGTAL_LIBRARY_DIRS - library directories for DGtal (normally not used!)
#  DGTAL_LIBRARIES    - libraries to link against
#  DGTAL_VERSION      - version of the DGtal library
@PACKAGE_INIT@

set(DGTAL_VERSION "@DGTAL_VERSION@")

include(CMakeFindDependencyMacro)

get_filename_component(DGTAL_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${DGTAL_CMAKE_DIR}/Modules")

#### Required dependencies  ####
find_dependency(Boost REQUIRED
  @Boost_HINTS@
  )
find_dependency(ZLIB REQUIRED
  @ZLIB_HINTS@
  )

set(WITH_EIGEN 1)
include(eigen)
set(DGtalLibDependencies ${DGtalLibDependencies} Eigen3::Eigen)


if(@GMP_FOUND_DGTAL@) #if GMP_FOUND_DGTAL
  find_package(GMP REQUIRED
    @GMP_HINTS@
    )
  set(WITH_GMP 1)
endif()


if(@ITK_FOUND_DGTAL@) #if ITK_FOUND_DGTAL
  set(WITH_ITK 1)
  find_dependency(ITK REQUIRED
    @ITK_HINTS@
    )
    include(${ITK_USE_FILE})
endif()

if(@CAIRO_FOUND_DGTAL@) #if CAIRO_FOUND_DGTAL
  find_package(Cairo REQUIRED
    @Cairo_HINTS@
    )
  set(WITH_CAIRO 1)
endif()

if(@HDF5_FOUND_DGTAL@) #if HDF5_FOUND_DGTAL
  find_dependency(HDF5 REQUIRED HL C
    @HDF5_HINTS@
    )
  set(WITH_HDF5 1)
endif()

if (@QGLVIEWER_FOUND_DGTAL@) #if QGLVIEWER_FOUND_DGTAL
  find_package(QGLVIEWER REQUIRED
    @QGLVIEWER_HINTS@
    )
  set(WITH_VISU3D_QGLVIEWER 1)
endif (@QGLVIEWER_FOUND_DGTAL@)

#-- We force the Qt rediscovering
if (@QT5_FOUND_DGTAL@) #if QT5_FOUND_DGTAL
  find_dependency(Qt5 COMPONENTS Widgets OpenGL Xml REQUIRED
    @Qt5_HINTS@
    )
  set(WITH_QT5 1)
endif (@QT5_FOUND_DGTAL@)

if(@OPENMP_FOUND_DGTAL@) #if OPENMP_FOUND_DGTAL
  if(APPLE)
    message("-- Setting OpenMP flags on MacOs. Assuming `brew install libomp`")
    if(CMAKE_C_COMPILER_ID MATCHES "Clang\$")
     IF(EXISTS "/opt/homebrew/")
      message(STATUS "Using M1/Homebrew C paths")
      set(OpenMP_C_FLAGS "-Xpreprocessor -fopenmp  -I/opt/homebrew/include" CACHE INTERNAL "OpenMP flags for #Xcode toolchain.")
      set(OpenMP_C_LIB_NAMES "omp" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
      set(OpenMP_omp_LIBRARY "/opt/homebrew/lib/libomp.dylib" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
      else()
      set(OpenMP_C_FLAGS "-Xpreprocessor -fopenmp -I/usr/local/opt/libomp/include" CACHE INTERNAL "OpenMP flags for Xcode toolchain.")
      set(OpenMP_C_LIB_NAMES "omp" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
      set(OpenMP_omp_LIBRARY "/usr/local/opt/libomp/lib/libomp.dylib" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
     endif()
    endif()
   if(CMAKE_CXX_COMPILER_ID MATCHES "Clang\$")
    IF(EXISTS "/opt/homebrew/")
      message(STATUS "Using M1/Homebrew C++ paths")
      set(OpenMP_CXX_FLAGS "-Xpreprocessor -fopenmp  -I/opt/homebrew/include"  CACHE INTERNAL "OpenMP flags for Xcode toolchain.")
      set(OpenMP_CXX_LIB_NAMES "omp" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
      set(OpenMP_omp_LIBRARY "/opt/homebrew/lib/libomp.dylib" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
    else()
      set(OpenMP_CXX_FLAGS "-Xpreprocessor -fopenmp -I/usr/local/opt/libomp/include" CACHE INTERNAL "OpenMP flags for Xcode toolchain.")
      set(OpenMP_CXX_LIB_NAMES "omp" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
      set(OpenMP_omp_LIBRARY "/usr/local/opt/libomp/lib/libomp.dylib" CACHE INTERNAL "OpenMP lib name for Xcode toolchain.")
     endif()
    endif()
  endif()
  find_dependency(OpenMP REQUIRED
         @OpenMP_HINTS@
         )
  set(WITH_OPENMP 1)
endif()




if(@CGAL_FOUND_DGTAL@) #if CGAL_FOUND_DGTAL
  find_dependency(CGAL COMPONENTS Core
    @CGAL_HINTS@
    )
  set(WITH_CGAL 1)
endif()

if(@FFTW3_FOUND_DGTAL@) #if FFTW3_FOUND_DGTAL
  find_package(FFTW3 REQUIRED
    @FFTW3_HINTS@
    )
  set(WITH_FFTW3 1)
endif()

if(NOT TARGET DGtal)
  # Our imported targets, including dependencies
  include ("${DGTAL_CMAKE_DIR}/DGtalLibraryDepends.cmake")
endif()

set(DGTAL_LIBRARIES DGtal Eigen3::Eigen)
get_target_property(DGTAL_INCLUDE_DIRS DGtal INTERFACE_INCLUDE_DIRECTORIES)
